Filter          ::= AndExpr ( <'or'> Filter )*
AndExpr         ::= Comp ( <'and'> AndExpr )*
Comp            ::= Attribute Op Value
                    | Value Op Attribute
                    | PropExpr
                    | WS <'('> Filter <')'> WS
Op              ::= '<' | '<=' | '=' | '>=' | '>' | '!='

Attribute       ::= WS #'[a-zA-Z_]\w*(/[a-zA-Z_]\w*)*' WS
<Value>         ::= IntValue | DateValue | StringValue | BoolValue
IntValue        ::= WS #'\d+' WS
DateValue       ::= WS #'\d+-\d+-\d+(T\d+:\d+:\d+(\.\d+)?(Z|[+-]\d+:\d+))?' WS
<StringValue>   ::= WS (DoubleQuoteString | SingleQuoteString) WS
BoolValue       ::= WS ('true' | 'false') WS
PropExpr        ::= WS <'property['> StringValue <']'> WS Op StringValue

<WS>            ::= <#'\s*'>

DoubleQuoteString ::= #"\"[^\"\\]*(?:\\.[^\"\\]*)*\""
SingleQuoteString ::= #"'[^'\\]*(?:\\.[^'\\]*)*'"
